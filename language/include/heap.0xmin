//heap
void #set:(){//documentation
	heap{
		#type int{i29};
		#type Function{
			@(...%[])->%[] & {@};    //non-macro(#) function(->) that takes in a list(...) of register names(%) that store memory addresses([]) and outputs it to a single output register(%) that stores a memory location([]).
			call:#(...%[])->@{};     //property 'call' is a macro function that takes in no arguments
			inputs:#{...%[]};//array of registers
			use:#()->Function mutate heap;//def's Function, by assigning it onto the heap
		};
		#type Page{
			next:@Page*;
			last:@Page*;
			isUsed:@bool;
			data:@any[];//+page == +page.data
		};
		//use '0' for null pointers
		#type Object: Page.data* on heap;
		regsUsed:#int & %;
		alloc:Function(%Object*?,length:%int,offset:%int);//not implemented yet

		malloc:Function(length:%int)->%Object*? & (%Object|#{%null*,flag(if sign,if overflow)});
		free:Function(%Object*)->void;
		
		resize:Function(%Object*,%int16)-> #{%int16,flag(if sign)};
		resizeBack:Function(%Object*,%int16)-> #{%int16,%Object*,flag(if sign)};
		
		clear:Function(%Object*,fillWith:{@} & {int16,int13}? & []static)->void;
		memset:Function(%Object*,%int16,%int13)->void;
		
		realloc:Function(%Object*,%int16,onMovePage:%(@()->bool & flag(if==0))*,%int16)->%Object|onMovePage();
		ref:#(%Object*)->{@};
		unref:#(%Object*,{@}?,#(!!label)?)->{@};
		memory{
			@Page;//start page
			#list of intended actions{
				#def: this;
				$undef: this;
				$def set: this;
			};
		};
	};
};
void #set: 0xmin.settings.language("tptasm");
void #let set: heap{
	#let: free,malloc,clear,memset,resize,resizeBack,realloc;//@functions
	#let: regsUsed = 3;//:int & %
	#let: ref,unref;//#()->{@}
	#let: memory;
	//each of the functions has a '.call(...args)' method
	#let: Call;
	#let set: memoryData(){
		#def: malloc;
		#def: free;
		#def:{
			#def: clear;
			#def: memset;
		};
		#def set: memory{
			0x1fffffff;//this line is not needed, it just makes finding the start of the heap easier to find when debugging
			#def: page0;
		};
	};
	#;
	#set: Call(foo,inputRegs){//call functionName,args
		#let: repeatNum = foo.nextReg.n + (foo.inputs..length-inputRegs..length);
		#let: i=0;
		repeat (foo.nextReg.n):{//TODO: don't push the input inputRegs
			@push %i;
			#set: i=i+1;
		};
		{//set inputs
			#let: i=0;
			repeat foo.inputs..length:{//assume {def a}[0] == {$def a;$set a}
				repeat !!inputRegs[+i]
				repeat #(+inputRegs[+i] != +foo.inputs[+i])
					@ %(foo.inputs[+i]) = %(inputRegs[+i]);
				repeat (!inputRegs[+i] && !!foo.inputs[+i].default)
					@ %(foo.inputs[+i]) = #(foo.inputs[+i].default);
				#set: i=i+1;
			};
			repeat !inputRegs[i];
		};
		call foo;
		repeat (foo.nextReg.n):{
			#set: i=i-1;
			repeat (+i>=foo.outputs..length):@pop %i;
			repeat (+i<foo.outputs..length):@sp+=1;
		};
		#let: startI=0;
		//UNFINNISHED
	}::{
		#let: addCaller(foo,len){
			//repeat (len==1)
			#let: heap_Call=Call;
			#let: foo.call(){
				...let set: heap_Call(foo,arguments);
			};
		};
	};
	#let intoHeapFunction(foo,argumentsLength){
		#set: foo.use(){
			#heap::{def foo};
			#return = foo;
		};
		#set: Call.addCaller(foo,argumentsLength);
	};
	#let: Page()={
		#def let: next{0};//:@* //pointer
		#def let: last{0};//:@* //pointer
		#def let: isUsed{0};
		#def let: data;//:@[] //array
	}::{
		#let: prototype=..this();
		#let: size=prototype..compile..length;
		#let: pointer()={
			...set let prototype;
		};
	};
	#let: Object()={
		...let set Page();
		$set this;
	}::{
		#let: prototype=..this();
		#let: size={
			ram => #(- Page.size);
			$def prototype;
		}..compile..length;
		#let: pointer()={
			...set let prototype;
		};
	};
	//note: Page=().isUsed can used as an int to count refernces for smart pointers.
	#let: page0=Page();//:@
	#set: page0{//start of the heap
		#set: next..splice(0,1,{0xffff});
		#set: last..splice(0,1,{(page0);});
	};
	#let: enum(start,inputsLen)={
		repeat !inputsLen #set: inputsLen=1;
		#let: newObj={};
		#set: newObj(){
			#set: return =+newObj.n;
			repeat #(newObj.n>=inputsLen):{
				#set: newObj.push{push newObj.n;};
				#set: newObj.pop..splice(0,0,{pop newObj.n;});
			};
			#set: newObj.n=newObj.n+1;
		}::{
			#let: n=start;
			#let: push{};
			#let: pop{};
		};
		#set: return = newObj;
	};
	#let isPageEndOfHeap(page){
		%page - 0xffff;
	};
	#let skipIfEndOfHeap(nextPage,do){
		//prevents mutating the end of the heap
		:block;
		isPageEndOfHeap(nextPage);
		jump -> block..return if<=0 !signed;
		def do;
	};
	//
	//%:weak,@:let,$:break,#:this
	#set: malloc{
		#let: nextReg=enum(0);
		#let: inputs{//:#
			#def let: words=nextReg();//:%int
		};
		#let: data{
			(page0) #def let: startPage;//:@Page*
		};
		#let: outputs{//:#
			#def let: pointer=Object.pointer=()<->0;//:%Object*
		};
		...let inputs;
		...let data;
		...let outputs;
		#let: page=Page.pointer=()<->nextReg();//:%Page*
		#let: pageSize=nextReg();//:%int; size of the current page
		#let: remainder=pageSize;
		#let: spaceNeeded=words;
		#let: extraSpace=Page.size;//:int; miminum page size
		%spaceNeeded += extraSpace;
		delete words;
		%page=[startPage];//Page* = [Page**]
		//%page=[%page];//Page = [Page*]
		#let: whenFound;
		#def let: loop{
			#let: failed;
			{//isUsed==0?
				:ifBlock;
				[%page+page.isUsed]|0;//%isUsed=[%page+page.isUsed];
				jump failed if!=0;
			};
			//assert: %page = next unused page
			{//hasEnoughSpace?
				%pageSize+=[%page+page.next];
				%pageSize-=%page;
				%remainder-=%spaceNeeded;
				jump whenFound if>=0 !signed;
			};
			#def: failed{
				%page=[%page+page.next];
				isPageEndOfHeap(page);
				jump->loop if>=0;
				{:outOfHeapMemory;
					//out of heap memory
					%pointer = 0;
					%pointer - 1;//sets the sign and overflow flags for an error
					return;
				};
			};
		};
		#def: whenFound{
			[%page+page.isUsed]=1;
			%remainder-=extraSpace;
			jump ..return if<=0;//if no space => dont need to rewire page pointers
			//%remainder+=extraSpace;
			delete remainder;
			//find location for nextPage
			#let: newPage<->pageSize;//:%Page*
			delete pageSize;
			%newPage=%page;
			%newPage+=%spaceNeeded;
			#let: nextPage<->spaceNeeded;
			delete spaceNeeded;
			%nextPage=[%page+page.next];
			[startPage]=%nextPage;//moves [startPage] to increase malloc's efficiency
			{//setUp page pointers;
				//a<-->c
				//a<->b<->c
				//a=r2,b=r4,c=r5
				#let: a=page,b=newPage,c=nextPage;
				[%b+page.isUsed]=0;
				[%b+page.next]=%c;//b->c
				skipIfEndOfHeap(c)<:{
					[%c+page.last]=%b;//b<-c
				};
				[%b+page.last]=%a;//a<-b aka: b.<- = a
				[%a+page.next]=%b;//a->b aka: a.-> = b
			};
			//setOutput
		};
		%pointer=%page;
		%pointer+=Page.size;
		return;
		#def: data;
	}::{void #set: intoHeapFunction(..this,1);};
	#set: free{
		#let: nextReg=enum(0);//get next free register
		#let: inputs{//:#
			#def let: pointer=Object.pointer=()<->nextReg();//:%Object*
		};
		#let: data{};
		#let: outputs{};
		#let: page=Page.pointer=()<->inputs.pointer;//:%Page*
		%page-=Page.size;
		[%page+page.isUsed]=0;
		#let: newLastPage=Page.pointer=()<->nextReg();
		#let: newNextPage=Page.pointer=()<->nextReg();
		{:joinNeighboursIfFree;
			{:block;
				:extendForward;
				%newNextPage=[%page+page.next];
				[%newNextPage+page.isUsed]|0;
				jump->block..return if!=0;
				%newNextPage=[%page+page.next];
			};
			{:block;
				:extendBackward;
				%newLastPage=[%page+page.last];
				[%newLastPage+page.isUsed]|0;
				jump->block..return if==0;
				%newLastPage=%page;
			};
		};
		{:reconnectPages;
			skipIfEndOfHeap(newNextPage)<:{
				[%newLastPage+newLastPage.next]=%newNextPage;
			};
			[%newNextPage+newNextPage.last]=%newLastPage;
		};
		{:moveStartingPage;
			let startPage=otherPage;//:[%]
			///[startPage]=%page if %page<[startPage];
			[malloc.data.startPage]-%newLastPage;
			jump->moveStartingPage..return if<=0;
			//assert:%startPage>%page
			[malloc.data.startPage]=%newLastPage;
		};
		return;
		#def: data;
	}::{void #set: intoHeapFunction(..this,1);};
	#set: sizeOf(pageInput){
		#let: lengthOutput=pageInput;
		//%pageInput=%lengthOutput;
		%lengthOutput-=[%page-(Page.prototype..length-Page.prototype.next)];
	};
	#set: clear{//aka fill memory
		#let: nextReg=enum(0);
		#let: self=..this;
		#let: inputs{
			#def let: page=nextReg();//input, page data
		};
		...let inputs;
		delete self;
		#let: data{
			#def let: redHalf{0};
			#def let: blueHalf{0};
		};
		#let offset;
		$offset=>Page.prototype.data;
		#let: address=nextReg();
		%address=%page;
		%page-=offset;
		mask = [data.redHalf];
		#let: fillData=nextReg();
		%fillData=[data.blueHalf];
		#def let: for{
			#let: i=nextReg();
			%i=[%page+Page.prototype.next];
			%i-=%address;
			#let: Loop(n)={
				let end;
				%i-=n;
				jump->end if<0;
				#def let: start;
				{
					let i=0;
					repeat n:{
						[%address+i]=%fillData;
						#i=i+1;
					};
				};
				%address+=n;
				%i-=n;
				jump->start if>0#def end;
				%i+=n;
			};
			#def: Loop(8);
			jump->..return if<=0;
			#def let: loop{
				[%address]=%fillData;
				%address+=1;
				%i-=1;
				jump->..this if>0;
			};
		};
		%page+=offset;
		return;
		#def: data;
	}::{void #set: intoHeapFunction(..this,1);};
	#set: memset{
		#let: nextReg=enum(0);
		#let: inputs{
			#def let: page=nextReg();//input
			#def let: high=nextReg();
			#def let: low=nextReg();
		};
		...let inputs;
		[clear.data.redHalf]=%high;
		[clear.data.blueHalf]=%low;
		call clear;//clear.call(inputs.page);
		[clear.data.redHalf]=0;
		[clear.data.blueHalf]=0;
		return;
	}::{void #set: intoHeapFunction(..this,3);};
	#set:resize{//similar to realloc except it doesn't change the pointer if there isn't any space
		//returns fail by setting the signed flag to 1
		#let: nextReg=enum(0);
		#let: inputs{
			#def let: object=Object.pointer=()<->nextReg();//:%Object* ; input
			#def let: size=nextReg();//:%int signed; size of new block
		};
		#let: outputs{
			#def let: size=0;//:%int ; returns the new size of the input Object
		};
		//...let inputs;
		//...let outputs;
		let nextPage = Page.pointer=()<->nextReg();//:%Page*
		let newPage = Page.pointer=()<->inputs.size;
		%newPage += %inputs.object;//assert: %newPage == %object + %size
		%nextPage = [%inputs.object - (-inputs.object.next)];
		let difference = nextReg();//:%int
		{:checkIfUsed;
			:ifBlock;
			let fail;
			isPageEndOfHeap(nextPage);
			jump -> fail if<=0;
			[%nextPage + nextPage.isUsed] | 0;
			jump -> ifBlock..return if==0;
			def fail{:noSpaceToExpand;
				%outputs.size = %nextPage;
				%outputs.size -= %newPage;
				return;
			};
		};
		//assert: [%nextPage] is free
		{
			let nextNextPage = Page.pointer=()<->nextReg();
			%nextNextPage = [%nextPage+nextPage.next];
			%inputs.object -= Page.size;
			let inputPage = Page.pointer=() <-> inputs.object;
			let mergePagesFully;
			{:checkForSpaceToInsertPage;
				%difference = %nextNextPage;
				%difference -= %newPage;//check for enough space for inputs.size
				%difference - Page.size;//check for enough space to insert a new page
				jump -> mergePagesFully if<=0 !signed;
			};
			let checkForSuccess;
			{:insertNewPage;
				[%inputPage+inputPage.next] = %newPage;
				skipIfEndOfHeap(nextNextPage)<:{
					[%nextNextPage+nextNextPage.last] = %newPage;
				};
				[%newPage+newPage.next] = %nextNextPage;
				[%newPage+newPage.last] = %inputPage;
				[%newPage+newPage.isUsed] = 0;
				jump -> checkForSuccess;//assert: sign flag == 0
			};
			def mergePagesFully{
				[%inputPage+inputPage.next] = %nextNextPage;
				skipIfEndOfHeap(inputPage)<:{
					[%nextNextPage+nextNextPage.last] = %inputPage;
				};
				//assert: sign flag: 1|0
			};
			def checkForSuccess{
				let temp = difference;
				%temp = [%inputPage+inputPage.next];
				%temp -= %inputPage;
				%temp -= %inputPage.data;
				%outputs.size = %temp;
				return;
			};
			//assert: function has returned
		};
	}::{void #set: intoHeapFunction(..this,2);};
	#set:resizeBack{
		//returns fail by setting the signed flag to 1
		#let: nextReg=enum(0);
		#let: inputs{
			#def let: object=Object.pointer=()<->nextReg();//:%Object* ; input
			#def let: size=nextReg();//:%int ; size of new block
		};
		#let: outputs{
			#def let: size=0;//:%int ; returns the new size of the input Object
		};
		//UNFINISHED
	}::{void #set: intoHeapFunction(..this,2);};
	#set:realloc{
		//TODO: redo this whole function
		...labelsof{:setUpMeta;
			#(){:pseudocode_for_interface;
				//expands memory in both directions or finds new block
				#let realloc(object,size,offset){//:({},int,int)->{}
					repeat offset: 0;
					...set: object..splice(0xmin.Math.max(-offset, 0), size);
					:newObject;
					#let: sizeLeft <-> size - newObject..length;
					repeat sizeLeft: 0;
					delete: newObject,sizeLeft;
				};
			};
			#let: nextReg = enum(0);
			#let: inputs{
				#def let: page = Page.pointer=() <-> nextReg();//:%Object* ; input
				#def let: size = nextReg();//:%int ; size of new block
				#def let: onMovePage = nextReg()::{let default = 0};//:%{@}*?|0 ; function pointer to call if there is a reallocation
				#def let: offset = nextReg()::{let default = 0};//:%int ; data offset. Is the new index for the current data, relative to the new page.
			};
			#let: outputs{
				#def let: newPage = 0;//:%Object* ; returns 0 if no page found
			};
		};
		... labelsof{:setUpVariables;
			...let: inputs;
			...let: outputs;
			let nextPage = Page.pointer=() <-> nextReg();//:%Page*
			let pageSize = nextReg();//:%int
			let maxIndexNeeded = nextReg();//:%int ; is the size - offset
			let relocate{
				${:ifBlock;
					%onMovePage |= 0;
					jump -> ifBlock..return if == 0;//handles %onMovePage == null
					call %onMovePage;//function '%onMovePage' should leave only 1 reference to '%page'
					let ..this.returner;
					jump -> returner if != 0;//bool
				};
				let nextReg = enum(0xmin.Math.max(free.nextReg.n,malloc.nextReg.n));
				let temp = nextReg();
				let tempRegs{//are not mutated by malloc or free
					#def let oldPage = Page.pointer=() <-> nextReg();
					#def let offset = nextReg();
				};
				%tempRegs.offset = %offset;
				%tempRegs.oldPage = %page;
				%malloc.inputs.(0) = %size;
				call malloc;
				let page = ¬,size = ¬, onMovePage = ¬, offset = ¬;//sets variables to null
				let newPage = malloc.outputs.(0);
				//assume: %page == %malloc.outputs.(0)
				{:moveData;
					#let nextReg = enum(newPage + 1);
					...let: tempRegs;
					let startI_oldPage <-> nextReg();
					let startI_newPage <-> nextReg();
					%startI_oldPage = %oldPage;
					%startI_newPage = %newPage;
					let i = offset;
					%temp = %offset;
					#offset = temp;
					%i = #(-1&0xffff);
					${:ifBlock;
						let rightShiftData;
						%offset |= 0;
						jump -> rightShiftData if >= 0;
						#def let: leftShiftData{//assert: offset < 0
							%startI_oldPage -= %offset;
							%i -= %offset;
						};
						jump -> ifBlock..return;
						#def: rightShiftData{//assert: offset > 0
							%startI_newPage += %offset;
						};
					};
					delete offset;//assert: +i == +offset
					%i += [%oldPage - (Page.size-oldPage.next)];
					%i -= %oldPage;
					//assert: %i = sizeof oldPage
					let copyingOverHighBits;//:#const bool
					let readHigh(rOut,rAddress){
						@(0x11100001|(rOut&0xf<<8)|(rAddress&0xf<<4));
					};
					let loopN(numOfRepsPerCheck,hasFirstCheck,hasNoLengthCheck)={
						let reps = numOfRepsPerCheck;//:#int
						repeat !!hasFirstCheck %i -= #(reps - 1) !store;
						repeat !!hasFirstCheck jump -> ..return if < 0;
						repeat !!hasNoLengthCheck jump -> moveData..return if == 0;
						${:loop;
							repeat reps:{
								repeat !!copyingOverHighBits:{
									%startI_oldPage += %i;
									readHigh(temp,startI_oldPage);
									%startI_oldPage -= %i;
									mask = %temp;
								};
								%temp = [%startI_oldPage + %i];
								[%startI_newPage + %i] = %temp;
								%i -= 1;
							};
							jump -> loop if >= 0;
						};
						jump -> moveData..return if < 0;
						i += reps;
					};
					loopN(8,{},{});
					loopN(1,(),{});
					repeat !!copyingOverHighBits mask = 0;
					repeat (nextReg.n > tempRegs[0]) throw "heap source code error: using too many registers";
				};
				{:callingFree;
					%free.inputs.(0) = %tempRegs.oldPage;
					let tempRegs.newPage <-> tempRegs.oldPage;
					delete tempRegs.oldPage;
					%tempRegs.newPage = %newPage;
					call free;
				};
				%outputs.newPage = tempRegs.newPage;
				return #def returner;
			};
		};
		${:extendForward;
			//checks for max size
			{:getPageSize;
				%nextPage = [%page - #(Page.size - page.next)];
				%pageSize = %nextPage;
				%pageSize -= %page;
			};
			%maxIndexNeeded = %size;
			%maxIndexNeeded -= %offset;
			%pageSize -= %maxIndexNeeded !store;
			jump -> extendForward..return if >= 0;//return if page is already big enough i.e. if(%pageSize >= %size)
			repeat 0 jump -> shrinkPage if < 0;//UNFINISHED
			[%nextPage + nextPage.isUsed] |= 0 !store;
			jump -> relocate if != 0;
			{:whenFree;
				let doublePageSize <-> pageSize;//size of both pages combined
				%doublePageSize = [%nextPage + nextPage.next];
				%doublePageSize -= %page;
				%doublePageSize -= %maxIndexNeeded !store;
				jump -> relocate if < 0;//note: do not need to consider merging more pages since consecitive empty pages should be imposible
			};
			{:merge2Pages;
				let temp <-> pageSize;
				let newNextPage = Page.pointer=() <-> maxIndexNeeded;
				%newNextPage += %page;
				delete maxIndexNeeded;
				[%page - #(Page.size - page.next)] = %newNextPage;
				%nextPage = [%nextPage + nextPage.next];
				${:updateNextNextPage;
					:block;
					%nextPage += 1 !store;//assume: there are 0x8000 memory locations
					jump -> block..return if sign;//prevents mutating memory outside the end of the heap.
					[%nextPage + nextPage.last] = %page;
					[%nextPage + nextPage.last] -= Page.size;
				};
				{//construct newNext
					[%newNextPage + newNextPage.next] = %nextPage;
					[%newNextPage + newNextPage.last] = %page;
					[%newNextPage + newNextPage.isUsed] = 0;
				};
				repeat 0 jump -> extendForward..return;//not needed since it is the same as '@jump => ram 1';
			};
		};
		repeat 0 ${:extendBackward;//There is probably a way to DRY this part
			let lastPage = Page.pointer=() <-> nextPage;
			let minIndexNeeded = offset;
			//checks for max size
			{:getPageSize;
				%lastPage = [%page - #(Page.size - page.last)];
				%pageSize = %lastPage;
				%pageSize -= %page;
			};
			let resizeThisPage;
			%minIndexNeeded |= 0;
			jump -> resizeThisPage if < 0;
			%pageSize -= %minIndexNeeded !store;
			jump -> extendForward..return if >= 0;//return if page is already big enough i.e. if(%pageSize >= %size)
			[%lastPage + lastPage.isUsed] |= 0 !store;
			jump -> relocate if != 0;
			{:whenFree;
				let doublePageSize <-> pageSize;//size of both pages combined
				%doublePageSize = [%lastPage + lastPage.last];
				%doublePageSize -= %page;
				%doublePageSize -= %maxIndexNeeded !store;
				jump -> relocate if < 0;//note: do not need to consider merging more pages since consecitive empty pages should be imposible
			};
			def resizeThisPage{
				let temp <-> pageSize;
				let newLastPage = Page.pointer=() <-> maxIndexNeeded;
				%newLastPage += %page;
				delete maxIndexNeeded;
				[%page - #(Page.size - page.last)] = %newLastPage;
				%lastPage = [%lastPage + lastPage.last];
				skipIfEndOfHeap(lastPage)<:{
					:updateNextNextPage;
					[%lastPage + lastPage.last] = %page;
					[%lastPage + lastPage.last] -= Page.size;
				};
				{//construct newNext
					[%newLastPage + newLastPage.next] = %page;
					[%newLastPage + newLastPage.last] = %lastPage;
					[%newLastPage + newLastPage.isUsed] = 0;
				};
				repeat 0 jump -> extendBackward..return;//not needed since it is the same as '@jump => ram 1';
			};
		};
		#nextReg.n <-> relocate.nextReg.n;
		return;
		def relocate;
		//UNFINISHED
	}::{void #Call.addCaller(..this,4);};
	//garbidge collection
		#set: ref(r_address){//#([%Page.data])->{@}
			//can be used when adding a reference
			[%r_address-#(Page.prototype.data-Page.prototype.isUsed)]+=1;
		};
		#set: unref(r_address,deconstruct,longVersion){//:#([%Page.data],{@}?,bool & label?)->{@}
			//can be used when removing a reference
			[%r_address-#(Page.prototype.data-Page.prototype.isUsed)]-=1;
			:block;
			jump->block..return if>0;
			delete block;
			def deconstruct||{};
			repeat 0:{//clear
				%clear.inputs.(0)=%r_address;
				push %r_address;
				%r_address=0xffff;
				[clear.data.redHalf]=%r_address;
				[clear.data.blueHalf]=%r_address;
				pop %r_address;
				call clear;
			};
			#let: nextReg=free.nextReg;
			repeat (+r_address!=+free.inputs[0]):%free.inputs.(0)=%r_address;
			repeat !!longVersion:free.call(r_address);
			repeat !longVersion:call free;
		};
	//----
	#def: memoryData();
};
void #set:(){//examples
	{:general_manyForms;
		{
			...labelsof {import lib "heap.0xmin";};
			let heap{import lib "heap.0xmin";};
		};
		{
			heap.malloc;
			malloc;
		};
		{
			call malloc;
			malloc.call();
		};
		{
			def heap;
			def heap.malloc;
			def heap.memory;
		};
	};
	{:example1;
		let doStuff{@;};
		...labelsof {import lib "heap.0xmin";};
		{
			let sizeOfObject = 5;
			@ r0 = sizeOfObject;//:size of object
			@ call malloc;
			@ [r0 + 0] = "h";
			@ [r0 + 1] = "e";
			@ [r0 + 2] = "l";
			@ [r0 + 3] = "l";
			@ [r0 + 4] = "o";
			@ call free;
		};
		{
			# let object2 = 1;//:%(Object)*
			@ r malloc.inputs.(0) = 40;
			@ call heap.malloc;
			@ r object2 = heap.malloc.outputs.(0);
			...doStuff;
			@ r free.inputs.(0) = r object2;
			@ call free;
			@ hault;
		};
		#def malloc;
		#def free;
		//#def realloc;
		#def memory;
	};
	{:example2;
		//note: heap.anyHeapFunction.call() saves registers onto the stack to make it easier to use.
		let heap{import lib "heap.0xmin";};
		let object = 0;//:%
		let size = 1;//:%
		r size = 10;
		heap.malloc.call(size);
		r object = heap.malloc.outputs.(0);
		heap.free.call(size);
		hault;
		def heap;
	};
	{
		...labelsof {import lib "heap.0xmin";};
		let a = 0;
		let deconstructor = {
			...doStuff;
		};
		heap.ref(a);
		heap.unref(a,deconstructor,{});
		def deConstructor;
		def heap.memory;
	};
};
void #...let set heap;