#"tptasm";
...labelsof (){
	#return{let
		heap = ¬,
		print = ¬,
		Static = ¬,
		static = ¬,
		readHigh = ¬,
		RegEnum = ¬,
		reg = ¬,
		getInput = ¬,
		meta = ¬,
	};
	//import lib "R2/class.0xmin";
	repeat !heap
	#heap = {import lib "heap.0xmin"};
	repeat !print
	#print = {import lib "R2/print.0xmin";...let set print;};
	...labelsof(){//Static: static memory management
		let private{let obj,allowRedefine};
		let Static(label)={
			repeat !!label:#this = label;
			#this::(obj,allowRedefine)={//:(obj:{@},#bool & #({}|(¬))) -> #{use:#()->@obj on static}
				...labelsof{
					let this.[¬private.obj] = obj;
					let this.[¬private.allowRedefine] = allowRedefine;
				};
				#{
					let this.[¬Static] = this;
					let obj[¬Static] = this;
					let this.[¬constructor] = this;
					let obj[¬constructor] = this;
				};
				repeat !!obj def obj;
				let: def()<-{//same as use, but to local scope's static
					//static.def:#()->this & mutation
					meta.unimplemented();
					#{
						let static = 
					};
				};
			};
			let ..prototype{//with: static.
				let use(){//:#()-> this & mutate static
					//assert: this:Static() & a single static label
					let obj = this[¬private.obj];
					let allowRedefine = this[¬private.allowRedefine];
					repeat (this..defs..length == 0 || !!allowRedefine)
						#this..constructor.memory::{def this};
					#return = obj;
					repeat 0 #let this.use = #(){#return = obj};
				};
			};
			let this.memory;
			let use(obj,allowRedefine){//:({@}|Static()(),#(()|{})?)->
				repeat !!obj[¬this] #return = obj[¬this].use();
				repeat !obj[¬this] #return = ..this=(obj,allowRedefine).use();
			};
		}::{
			#void(){:exmples;
				let static = Static=();
				call static.use()<:{r0 = r1; r0 += r2; return};
				let string = static.use("hello world");
				r0 = string.use().(0);
				hault;
				def static.memory;
			};
		};
		#return = {let Static = Static};
	}();
	let static = Static=();
	let readHigh(rOut,rAddress){
		@(0x11100001 | (rOut & 0xf << 8) | (rAddress & 0xf << 4));
	};
	...labelsof(){//Reg: register management
		let type_register{//private properties
			let isUsed;
			let isConst;
		};//type Register = % & ( RegState()() | RegEnum()() )
		let RegEnum_forEachReg(from_ignore,static_regStorage,startI,nextI,do_stack,do_static)<-{//:injectable macro #(#%[],@{}?,#macro,#()->index & mutation,#macro,#macro)->{@}
			//from_ignore: #int & %[]? ; number of registers to ignore starting from @r0 
			//static_regStorage: ({@} on static)?
			#let from <-> +from_ignore || 0;
			#let ignore <=> {...codeof from_ignore};
			...run startI;
			let state;
			repeat (this - from):...codeof{
				#nextI();
				repeat (!¬ignore..indexOf(+i))
					meta.if(!static_regStorage)
					<:(){...run do_stack}
					<:(){
						$state => static_regStorage + i;
						...run do_static;
					};
			}
		}::{
			//this function is used by push and pop
			//even though it used macros and '...run' (aka macro injection), it is private, and only used in a similar scope so it should not be too much of an issue.
		};
		//type Template<T>:T() & {@} 
		let RegEnum(start)={//register enum
			//a consecitive block of used registers starting from r0.
			#this<->(start||0);
			//assert: +this == next free regester
			//assert: +this == number of registers used
			let freeRegs = {};//:%[]
			recur 1 let maxUsed,max = maxUsed = ..constructor(this) || ({} <-> +this);//highest register used by this RegEnum instance
			recur 1 let minUsed,min = minUsed = ..constructor(this) || ({} <-> +this);//lowest register used by this RegEnum instance
			(label,object){//:({#}?,{@}?&Template)->RegEnum()
				#return = #(arguments[0] || return);
				#meta.if(freeRegs..length > 0)<:(){
					#return <-> +freeRegs..splice(-1,1)[0];
				}<:(){
					#return <-> +..this;
					#..this<->..this + 1;
				};
				#maxUsed <-> 0xmin.Math.max(maxUsed,..this);
				#let return[¬..constructor.type_register];
				#return..constructor = ..this;
				repeat (+return >= (16-2)):throw "out of registers";
				
				repeat !!object #{
					#return<=>{
						...labelsof codeof (return...object);
					};
					#let return..proto = {}::{
						#..proto = {}::{
							let ..proto = return..proto;
							let ..super = return..super;
						};
						#..super = {}::{
							let ..proto = object..proto;
							let ..super = object..super;
						};
					};
				};
			};
		}::{
			let type_register = type_register;
			//RegEnum: provides a simple form of register management.
			let ..prototype{
				let pointer(data){//#()->%*
					meta.unimplemented("Unsertain where (which module/object) to put this function, or how it should be called");
					//'label:>reg.pointer({1;2})'
					#return = arguments[-2]||this()::{
						let data = data;
					};//note: || does won't always call 
					#value = arguments[-1]||{};
					#return::{
						type
						...labelsof {}<=>data¬..compile;
					};
				};
				let free(register){
					#this.freeRegs..splice(-0,0,{register});
				};
				let use(registerNumber){
					let index = this.freeRegs..indexOf(+registerNumber);
					repeat !!index #return = this.freeRegs..splice(index,1);
					repeat !index throw "register is already being used";
				};
				let push(from_ignore,static_regStorage){
					...codeof RegEnum_forEachReg<-(from_ignore,static_regStorage)
						<:(){let i <-> -1 + from;}
						<:(){#i=i+1;}
						<:(){@push %i;}
						<:(){[%static_regStorage + i] = r i;}
					;
				};
				let pop(from_ignore,static_regStorage){
					...labelsof codeof RegEnum_forEachReg<-(from_ignore,static_regStorage)
						<:(){let i <-> this;}
						<:(){#i=i-1;}
						<:(){@pop %i;}
						<:(){[%static_regStorage + i] = r i}
					;
				};
				let copy(){//:#()->RegEnum
					#return = this..constructor(this);
				};
				let blockScope(do_ignore,do){//:#((ignore:#int? & %[])?,do:#(RegEnum,ignoredRegs)->{@})->{@} ; allows use of all registers
					let do,ignore;
					meta.if(!!arguments[1])<:(){//blockScope(ignore,do)
						#do = arguments[1];
						#ignore = arguments[0];
					}<:(){//blockScope(ignore,do)
						#do = arguments[0];
						#ignore = #();
					};
					let regEnum = RegEnum(ignore || 0);
					let block = do(regEnum);
					//this..constructor(0xmin.Math.min(+this,+regEnum)<=>this)
					def let push = this.push(ignore || ());//ignores the first (ignore) registers from popping
					def block;
					def let pop = this.pop(ignore || ());
				};
				let weakScope(do){//:#(#(RegEnum)->{@})->{@} ; zero cost abstraction version of blockScope.
					let reg = this.copy();
					do(reg,this);
					#this.maxUsed <-> 0xmin.Math.max(+this.maxUsed,+reg.maxUsed,+reg);
				};
				let unsafeScope(do){//:#(#(RegEnum)->{@})->{@} ; same as weakScope, but doesn't mutate the original RegEnum
					let reg = this.copy();
					do(reg,this);
				};
			};
			let type(reg){
				let set reg[¬type_register];
			};
			let scope = {
				let borrow(regSet,do){//regSet:#int&%[],do:#(RegEnum)->{@}
					let regEnum = RegEnum(+regSet)::{
						#freeRegs<=>regSet;
					};
					do(regEnum);
				};
			};
			let isRegister(register){
				#return = register[¬type_register];
			};
		};
		let reg = RegEnum();
		#return = {let
			RegEnum = RegEnum,
			reg = reg,
		};
	}();
	let: CaseSet(){
		let match(){
			repeat arguments..length
		}
	};
	let getInput(outputReg,await_bool,doWhile_wait,miniProcess,portReg){//:#(%?,#?,{@}?,{@}?,%?)->{@} & @mutate terminal
		repeat !await_bool : awaitInput(outputReg,doWhile_wait,miniProcess,portReg);
		repeat !!await_bool: checkInput(outputReg,miniProcess,portReg);
	}::{
		let awaitInput(outputReg,doWhile_wait,miniProcess,portReg){//:#(%?,{@}?,{@}?,%?)->{@} & @mutate terminal
			let hasPort = !!portReg;
			let hasOutputReg = !!outputReg;
			repeat !+hasPort #{
				#portReg = outputReg||0 +1 %14;//:%
				let storeRegs;
			};
			repeat !+hasPort {
				push %portReg;
				%portReg = 0;
			};
			def let check;
			def let waitLoop{
				repeat !!doWhile_wait def doWhile_wait||{};
				wait %portReg;
				jump -> waitLoop if sign;
			};
			def let bump{bump %portReg};
			repeat (miniProcess..@..length > 14) throw "mini process too long for R2 IO";
			repeat !!miniProcess def miniProcess;
			repeat !!+hasOutputReg def let getOutputLoop{
				recv %outputReg %portReg;
				jump -> getOutputLoop if !carry;
			};
			repeat !+hasPort pop %portReg;
			let testInput(string)=>{//(string,%)->{@} ; builder pattern
				let i;
				let tempReg = 0;
				:self;
				push %tempReg;
				%tempReg = [i];
				%tempReg - string..length;
				{:block;
					jump->block..return if >= 0;//when out of chars, use user input as normal
					[i] += 1;
					def doWhile||{};
					%outputReg = [string + %tempReg];
					pop %tempReg;
					def miniProcess||{};
					jump -> self..return;
					{:data;
						def string;
						@0 #def i;
					};
				};
				pop %tempReg;
				def return;
			};
		};
		let await = awaitInput;
		let checkInput(outputReg,miniProcess,onSome,onNone,portReg){//:#(%?,{@}?&@[0-14]?,{@}?,{@}?,%?)->{@} & @mutate terminal
			awaitInput(outputReg,(),miniProcess,portReg)::{
				undef waitLoop;
				delete waitLoop;
				#check::{
					wait %portReg;
					jump->onNone if sign;//TODO add on success and on fail
				};
				def onSome;
				jump-> onNone..return;
				def onNone;
			};
		};
		let check = checkInput;
	};
	let awaitInput = getInput.awaitInput;
	let checkInput = getInput.check;
	...labelsof(){
		let for(i,start,condision,next,do)={//(?%,?#(%)->{@},?#(%,$(break|continue),isContinue:#int&bool)->{@},#(%)->{@})
			#(){
				#(
					?i:%&#{isFree:#?},
					start:#(i:%)->{@},
					condision:#(i:%,to:@,isJumpOnFail:#?)->{@;@jump->to if flag;},
					do:#(i:%,break:@)->{@}
				)->{@}|
				|#(//for.auto
					RegEnum&()->%,
					(%usize,RegEnum,#@,#@)->{
						{@},
						{@}->@jump,
						@
					}
				)->{@}
			};
			repeat(arguments..length == 2)...run(){
				#return = constructor.auto(...arguments);
				break;
			};
			#i = arguments[-5]||(¬);
			#start = arguments[-4]||(i){%i = 0};
			#condision = arguments[-3]||(i,continue,isBreak){
				repeat !!isBreak ...codeof{
					let: breakAddress = continue;
					if < 0 !signed jump -> breakAddress;
				};
				repeat !isBreak if >= 0 !signed jump -> continue;
			};
			#next = arguments[-2]||(i){%i += 1};
			#do = arguments[-1]||(i){};
			let hasReg = !!i;
			let isSaveReg = !hasReg||!i.isFree;
			repeat !+hasReg:{
				#i = 1;
			};
			repeat !+hasReg push %i;
			def let start = start(i);
			let entry;
			def let jumpToEntry;
			recur do..flat def let loop{
				let jumpTo;
				def jumpTo;
				def let do = do(i,..return);
				def let next = next(i);
				def let condision = condision(i,jumpTo,isJumpToContinue);
			};
			#jumpToEntry{
				jump->loop.condision;
			};
			...labelsof loop;
			repeat !+hasReg pop %i;
		}::{
			let ..prototype{

			};
			:this;
			let nextFor(i,start,condision,next,do)={//runs `next` at the end of `start`
				delete this;
				...run this;
				jumpToEntry<=>{jump->loop.next};
			};
			let doFor(i,start,condision,next,do)={//1 cycle faster version of `for`
				delete this;
				...run this;
				undef jumpToEntry;
			};
			let auto(regEnum,argsFunction,doFunction){//for.auto(reg)<:(i){i = 10;if >= 0;i -= 1}
				#(){
					#(
						RegEnum&()->%,
						(%usize,RegEnum,#@,#@)->{
							{@},
							{@}->@jump,
							@
						},
						?(%usize,RegEnum,#@,#@)->{@},
					)->{@}
				};
				let reg = regEnum;
				let i;
				repeat !!reg #{
					let bool = RegEnum.isRegister(reg);
					repeat!!RegEnum.isRegister(reg): #i = reg;
					repeat!RegEnum.isRegister(reg): #i <-> reg();//default
				};
				#i::{%..this};
				let entry,loop,exit;
				let args = argsFunction(i,reg,exit,entry);
				let start=args[0],condision=args[1],next=args[2],do=doFunction()||args..splice(3,-1);
				delete args;
				def start;
				jump->entry;
				def loop;
				def do;
				def next;
				repeat !entry..defs..length def entry;
				@:$condision loop;
			};
		};
		let for_start_repeat(i_reg,getLength,do){//:#(%usize,{@}->%usize?,{@})->{@} ; similar to doFor
			getLength;
			def let loop{
				def do;
				%i_reg -= 1;
				jump -> loop if >= 0 !signed;
			};
		};
		let for_repeat(i_reg,length,do){//:#(%usize,#usize,{@})->{@}
			def let start{%i_reg = #(length - 1)};
			def let loop{
				def do;
				%i_reg -= 1;
				jump -> loop if >= 0 !signed;
			};
		};
		#return = {let for=for,for_start_repeat=for_start_repeat,for_repeat=for_repeat;};
	}();
	let meta{//{[string]:#(...)->{#}}
		let: enum(startValue)={
			#..this<->start||0;
			(value)={
				#..this<->..constructor;
				#..constructor<->..constructor+1||value;
			};
		};
		let: if(condision,then,else)<-{//:#(bool&number,#()->#,#()->#)
			delete arguments,scope,return;
			repeat !+condision #then = else;
			delete condision;
			delete else;
			...run (){#return = then;delete then}();
		};
		let: forEach(array,doFunc)<-{
			#let .["i"] = -1;
			#repeat array..length $doFunc(array[.["i"] = .["i"] + 1],.["i"],array,return);//v,i,a,s
			#delete .["i"];
			#delete array,doFunc;
			#delete arguments,scope,return;
		};
		let: repeat(length,doFunc)<-{//:#(#int,#()->{any})
			#let arguments.i = -1;
			repeat length $doFunc(arguments.i = arguments.i + 1);
			#delete i;
		};
		let assert(bool,message){
			repeat !+bool throw ("assertion failed" ... message);
		};
		let todo(message){
			print("\p00\c04ERROR:TODO:");
			print(message);print(";\c0f");hlt;
		};
		let unimplemented(reason){//:#(reason:#string?)->{}
			throw reason;
		}::{
			let test(testString){//:#(testString:#string?)->{@}
				repeat !!testString: print(testString);
			};//
		};
		let test(code)<-{
			...code;
		}::{
			let breakPoint(string){
				repeat !!string:print(""...string);
				@hlt;
			};
		};
		let inject(refLabel,do){#;
			return = refLabel;
			let oldSupertype = refLabel..super;
			refLabel..super = arguments;
			refLabel::{
				...run do;
			};
			refLabel..super = oldSupertype;
		};
	};
	repeat 0 ...labelsof (){//Types
		meta.unimplemented();
		let type(type)={
			#type::(){
				repeat (arguments[0]&&):{
					;
				};
				#.return = arguments[0] || return;
				#.this <=> .this;
				repeat 
				#.this[¬constructor];
			};
		}::{
			#this<-();
		};
		let String{
			let Len(obj)={
				def let length;
				def let string;
			}:>type();
			let Null(obj)={
				def let string;
				def let null = "\x00";
			}:>type();
		};
		#return = {
			let String = String
		};
	}();
}();