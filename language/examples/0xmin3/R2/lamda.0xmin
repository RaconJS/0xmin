#"tptasm";#"len";#"table";
let lamda(){//01010011
	/*(//hello_world
		v
			^<< << <<^<< << <<  p//H 1001000
			>> >>^<< << <<^ << <<^p//e 1100101
			^>> >> >>^<<^<< <<  p//l 1101100
			p//l 1101100
			>> ^ << ^ p//o 1101111
			v   ^<< << << << << p//"" 100000
			v^<<^<<^<< <<^<<^<<^p//w 1110111
			v^<<^<< <<^<<^<<^<<^p//o 1101111
			v^<<^<<^<< << <<^<< p//r 1110010
			v^<<^<< <<^<<^<< << p//l 1101100
			v^<<^<< << <<^<< << p//d 1100100
		v n
	)*/
		(and>or>t>f>+>*>
			(a>b>a)(or (and t f) t (+ f f) (* t f)) p n
			//(* (f>x>f(f(f x))) t)(a>p) n
		)
		(a>b>a b (a>b>b))//and
		(a>b>a (a>b>a) b)//or
		(a>b>a)//t
		(a>b>b)//f
		(a>b>(f>x>a f (b f x)))//+
		(a>b>(f>x>a (x>b f x) x))//*
}::{//p = print
	def let val{"0"};
	def let .["p"] {r1=0;send r1 [val]};
	def let .["<<"] {[val]<<=1};
	def let .[">>"] {[val]>>=1};
	def let .["^"] {[val]^=1};
	def let .["v"] {[val]=0};
	def let .["n"] {};
	delete val;
};
let heap{import lib "heap.0xmin";};
...(){
	let nextAddress{
		//#heap..splice(0,-0,{def ..this});
		0;
	};
	let m_class(class,argsLen,classConstructor,classDeconstructor)={
		//class:#Struct,constructor:#(label?,arguments?:any)->{@}
		let m_class=this..constructor;
		#class::{
			#class..proto=m_class..prototype;
			//#..splice(0,0,{});
			()={
				repeat !!arguments[0]:{#;
					return=#(arguments[0]<->this)::{
						#;
						..splice(0,-0);
						...codeof this;
						...labelsof this;
					};
					return..proto=..proto;
					return..super=..super;
					return..prototype=..prototype;
					return..supertype=..supertype;
				};
				repeat !return[¬m_class..prototype.static]:#..compile;
			};
			def let delRef<=>{
				let reg:>..this=()<->0;//:%this*
				let isFirstRef;//:#{}|#(¬)
				heap.unref(reg)<:{
					push reg;
					let i=-1;
					repeat ..this..length:{
						let ref=..this[i+=1][¬Ref];
						repeat !!ref:{:block;
							repeat !isFirst:r thisReg=[sp];
							repeat ref.canBeNull:jump->block..return if==0;
							r thisReg=[r thisReg+thisReg.parent];
							#isFirstRef=¬;
						};
					};
					pop reg;
					:unrefRefs;
					repeat !!isFirstRef:#unrefRefs<=>{};
				};
				return;
			};
		};
		#let set class..prototype::{
			let Ref(label,class)={#;
				//used for class().unref();
				//label has type [class*] on heap
				let label[¬constructor]=class;
				return = label;
			};
			let new(){//:(...%[])->push[Context*]
				let rMemory = heap.malloc.inputs[0];
				r rMemory=this..length;
				let rMemory = heap.malloc.outputs[0];
				repeat 0 {:malloc;
					//quicker O(1) way of allocating memory
					let address= rMemory+1;//[%]*
					r address=r rMemory;
					r rMemory = [nextAddress];
					r rMemory += heap..return;
					[nextAddress] += r address;
				};
				repeat 1 call heap.malloc;
				#let i=this..length-1;
				repeat !argsLen:let argsLen=this..length;
				def let assigner = {
					repeat argsLen:{
						[r rMemory+i]=pop;//r arguments.(i);//argument
						#i=i-1;
					};
				}
				:definer;
				let output=rMemory;//:%Object* aka %heap.Page*
				def classConstructor(definer)||{};//using $(¬)() throws an 'undeclared label' error
			};
			let addRef(reg){
				heap.ref(reg);
			};
			#new::(){
				push r rMemory;
			};
			let del(){//:pop[this:Context*]
				def classDeconstructor->()||{};
				call heap.free;
			};
		};
		#return=class;
	}::{
		let ..prototype{
			let static(label,valss){//:@[this] aka {@}
				#repeat !label: :label;
				let label[¬static];
				let label.new=¬;
				let label.addRef=¬;
				let label.delRef=¬;
				#return=this=(label,valss);
			};
			let ptr(label,vals){//:@[%this] aka %{@}*
				#repeat !label: :label;
				let label[¬ptr];
				#return=this=(label,vals);
			};
		};
	};
	let setup()->{delete setup;};
	let Context()={//when function called
		def let argument{0}:>Ref(FunctionInstance);//:FunctionInstance*||{InbuiltFunction*}*
		def let parent{0}:>Ref(Context);//:Context*
	}:>m_class(2,{})::{
		//used as %Context*
		#..prototype::{
			let unref(thisReg)={
				#thisReg=thisReg||0;
				#thisReg<=>Context=();//:%heap.Page*
				//assume %thisReg == &this;
				let loop;
				let loop1;//unrefs FunctionInstance
				push loop..return;
				//def loop1;
				def loop{
					push r thisReg;
					heap.unref(thisReg)<:{
						def:{:block;
							r thisReg=[r thisReg+thisReg.argument];
							call loop1;//unref FunctionInstance
						};
						def:{:block;
							r thisReg=[sp];
							r thisReg=[r thisReg+thisReg.parent];
							jump->block..return if==0;
							call loop;//unref Context
						};
						r thisReg=[sp];
						repeat 1{
							[r thisReg+0]=0;
							[r thisReg+1]=0;
						};
					};
					pop r thisReg;
					return;
				};
			};
		};
		#setup::(){
		};
	};
	let FunctionInstance()={//'(a>a)' when function created polymorph of Context
		def let function{0};//:Function* && {@}*
		def let parent{0}:>Ref(Context);//:Context*
	}:>m_class(2,{})::{
		//used as [sp+FunctionInstance*]
		#..prototype::{
			let unref(thisReg,unrefContext)=>{
				#let thisReg={}<->thisReg;
				#thisReg<=>FunctionInstance=();//:%heap.Page*
				//assume %thisReg == &this;
				let loop;
				push loop..return;
				repeat !!unrefContext:def unrefContext.loop1;
				def loop{
					push r thisReg;
					heap.unref(thisReg)<:{
						def:{:block;
							r thisReg=[r thisReg+thisReg.parent];
							jump->block..return if==0;
							repeat !!unrefContext:
								call unrefContext.loop;
							repeat !unrefContext:Context().unref(thisReg);
						};
						r thisReg=[sp];
						repeat 1{
							[r thisReg+0]=0;
							[r thisReg+1]=0;
						};
					};
					pop r thisReg;
					return;
				};
			};
		};
	};
	#setup();
	//note: function must come first so that 'addFoo()' works quicker.
	//note: argument must come first so it isn't assigned when 'new' is called
	let spareReg=heap.malloc.nextReg.n;
	let for_repeat(repeatNum,reg_i,do){//#(int,%,{@})->{@}
		repeat !!+repeatNum:{
			let i=reg_i;
			r i=repeatNum;
			def let loop{
				def do;
				r i-=1;
				jump->..this if > 0;
			};
		};
	};
	let getRecur(code){
		#return = code..flat..length;
	};
};
//note: 'a b > c d' --> 'a (b > (c d))'
//m_ == # aka meta
let parse(code,i,function,hasRecur,output)={//:(#(){...lambda code},builtInFunctions:{[""]:{@}})->{@}
	let returnValue;//:function
	//!!hasRecur => recur>1
	let line=code;
	repeat !hasRecur:{
		#line=code[0][0];
		#output={//extension object
			:this;
			let maxRecur=getRecur(line);
			let mainFoo=function=newFoo=({});
			let inputFunctions={...let set arguments[1]};
			let parent0;
			let contexts={
				def let refs;
				repeat(inputFunctions..labels..length==0): 1;//ref counter
				//assume: only '[parent0-1]' is used
				virtual:{#def parent0=Context=()::{let refs=refs}};
			};
			let functions{};
			def mainFoo<=>{...let mainFoo;}::{
				#let list=inputFunctions..labels,i1=0;
				repeat list..length:{#;
					let foo=inputFunctions[""...list[i1]];
					repeat!foo[¬newFoo]:{
						let contextReg=function.registers.context;//:%Context*
						let context=FunctionInstance=()::{#function<=>{@:foo;}};
						let foo[¬newFoo]=context;//mark the input functions
						#set contexts::{
							#def let context.refs;
							1;//ref counter
							def context;
							ram=>-1;7;
						};
						foo..splice(0,0,{//null;
							push [r contextReg + contextReg.argument];
						});
						foo..splice(-0,0,{jump->[sp+1]});
						let mainFoo.scope[""...list[i1]]=foo;
						//push end;
					};
					#set contexts::{ram=>-1};
					i1+=1;
				};
				{:main;
					let end{return};#end<->0;
					r newFoo..prototype.registers.context = parent0;//:virtual parent0
					def block;
					def let ..this.getAns{
						let ans=2;//:%
						let ret=3;
						pop r ans;
						repeat 0 {
							pop r ret;
							push r ans;
							push r ret;
						};
					};
					#stackLevel=0;
					return;
					def contexts;
					def inputFunctions;
					undef newFoo;
					def newFoo;
				};
			};
			delete this;
			//$undef mainFoo;
		};//:0xmin[]
		repeat !line:#:{}[]ERROR;
		#i=0;
	};
	let oldI=i;
	#i=+i;
	let args=0;
	recur output.maxRecur:
	repeat (i<line..length&&(line..length-oldI)): {
		let word=line[i];
		let continue=1;//:bool
		repeat (word=="("): {//zero cost abstraction
			#i+=1;
			repeat !!line[i][1]
				#:{}[]ERROR;//silent error
			recur output.maxRecur: 
			repeat !!line[i]: #returnValue=parse(line[i][0],0,function,1,output);
			#i+=2;
			#continue=0;
			#args+=1;
		};
		let word=line[i];
		repeat ((args<2) && (line[i+1]==">")):{//function a>b ==> (a){#return=b}
			#i+=2;
			let foo1=newFoo({},function,""...word);//:{@}
			#returnValue=parse(line,i,foo1,1,output);
			{//removes repeated functions
				let r=output.functions..length;
				let i1=0;
				repeat r:{
					repeat ((""...output.functions[i1].string)==#(""...foo1.string)):{
						#r=0;//r = i1+1; aka break;
						$undef foo1;
						#foo1=output.functions[i1];
					};
					#i1+=1;
				};
				repeat (+r==output.functions..length):{//if it is a new function
					#set output::{
						//null;//start of function
						def foo1;
					};
					#output.functions..splice(-0,0,{$def foo1});
				};
			};
			#function.addFoo(foo1);
			#set function.string{"(";...codeof foo1.string;")"};
			#continue=0;
			#args+=1;
		};
		let word=line[i];
		repeat ( (args<2) && !!word && (word!="(") && (word!=">")):{//word
			let value=function.scope[""...word];
			repeat !value:debugger (""...word);
			repeat !value#:{}[];//ERROR:value not defined
			#returnValue=function.addArg(value,function,output);
			#args+=1;
			#i+=1;
		};
		repeat(args>1):{
			#args-=1;
			#function.addCall();
		};
	};
	#oldI<->i;
	repeat !hasRecur repeat (args==0) #function.getAns<=>{};
	repeat !!hasRecur:#return = returnValue;
	repeat !hasRecur:#return = output;
}::{
	let set ..prototype{
		let newFoo(code,function,argName)={//:Function
			let stackLevel=function.stackLevel+1||0;
			let scope{
				repeat !!argName:let ..this[""...argName]<->stackLevel;
				let ..proto=function.scope;
			};
			let numOfArgs=0;
			let block={...codeof code};
			let string=">";
			//this program does not clean up it's memory usage.
			//I have not learnt how to do that yet. This is fine for smaller lamda expressions.
			let addFoo(arg){//arg from heap
				//note: type Function == type {@} & @(pop[arg:Context*])->push[return:private*,ans:Context*]
				//arg:Function*
				#set block{
					def let addFoo{
						//new Context
							push arg;//function:Function*
							call newFoo.makeContextInstance;//:(pop Function*,Context*)->push Context*
					};
				};
				#return=arg;
			};
			let addArg(arg,scope,output){//arg on stack 'arg'
				//arg:index, scope:
				let isNormalArgument=!arg[¬newFoo];
				#set block{
					def let addArg{
						repeat !!+isNormalArgument:...set{
							let currentContext=+registers..length;//:%Context*
							#currentContext = Context=() <-> currentContext;
							let argNumber=getArg(arg,scope);
							#set string{...(" "...+argNumber);};
							let getParentContext(){
								repeat !getParentContext.isNotFirst:
									r currentContext = [r registers.context + registers.context.parent];
								repeat !!getParentContext.isNotFirst:
									r currentContext = [r currentContext + currentContext.parent];
								let getParentContext.isNotFirst;
							};
							#let fast=1;
							repeat !!fast: repeat argNumber:getParentContext();
							repeat !fast:...(){
								let i=2;//:%
								for_repeat(argNumber,i)<:getParentContext
							};
							let arg<->currentContext;
							repeat (+argNumber!=0):
								r arg=[r currentContext + currentContext.argument];
							repeat (+argNumber==0):
								r arg=[r registers.context + currentContext.argument];
							push r arg;//:push Context*
							heap.ref(arg);
						};
						repeat !+isNormalArgument:...set{//arg is an input function
							#set string{...("i"...+output.inputFunctions..indexOf(arg));};
							push #(arg[¬newFoo]);//:push Context*
							[arg.(¬newFoo).refs]+=1;//heap.ref(arg)
						};
					};
				};
				#return=arg;
			};
			let addCall(){//stack={foo} //'(foo>...) arg'
				#set string{"c"};
				#set block{
					def let addCall{
						//assume: stack has [foo:Context*,arg]
						call newFoo.function;
					};
				};
			};
			let getArg(arg,scope){//assert:arg<=scope.stackLevel
				#return = scope.stackLevel-arg;
			};
			{
				def block;
				jump->[sp+1];
			};
		}::{
			let ..prototype={
				let swap{
					pop r1;//arg
					pop r2;//foo
					push r1;
					push r2;
				};
				let registers={
					def let context=Context=()<->0;
					def let fooInstance=FunctionInstance=()<->0;
				};
				let makeContextInstance{
					let ret=+spareReg;
					pop r ret;
					push r registers.context;//parent:Context*
					heap.ref(registers.context);
					def let definer=FunctionInstance=().new();
					let newContext=FunctionInstance=()<->definer.rMemory;//:%Context*
					//assert:[newContext.parent] == context;
					r registers.context = [r newContext+newContext.parent];
					jump->r ret;
				};
			};
			let prototype=..this=();
			def let function{}<=>{
				...let ..prototype;
				//assume: stack has [foo:FunctionInstance*,arg:FunctionInstance*,ret:*]
				//:@(pop[foo,arg,ret])->push[ans]
				push r registers.context;//callee's context
					//stack [foo,arg,ret,parent]
					//assume: context=r0;
					let stack_functionInstance = 3;
					{//{@} & ()->push[ans]
						let temp = registers.context+1;
						let arg_stack = 2;
						r registers.fooInstance = [sp+stack_functionInstance];
						{:copyContext;//does: context=new Context(context);
							let arg=temp;
							r arg = [sp+arg_stack];//arg
							push r arg;
							let parent=temp;
							r parent = [r registers.fooInstance+registers.fooInstance.parent];
							push r parent;//parent
							def heap.ref(parent)::{
								r parent|=0;
								#..splice(0,0,{jump->..return if==0})
							};
							registers.context.new();
							pop r registers.context;
						};
						//move arg reference from stack to heap
						let fooInstance=FunctionInstance=()<->temp;
						r fooInstance = [sp+stack_functionInstance];
						call [r fooInstance + fooInstance.function];//foo.function:[{@}]
					};
					//stack [foo,arg,ret,parent,ret1,ans]
					//assert %registers.context == newContext
					repeat 1{//handle unrefs
						let arg_stack=4;
						let parent_stack=2;
						let foo_stack=5;//original functionInstance
						//unref(newContext,context);
						def let removeContext=Context=().unref(registers.context);//removed from register
						//do not need to remove arg ref from stack as it bellongs to the context on the heap and would be unrefed by this point.
						r registers.context = [sp + foo_stack];//removed from stack
						FunctionInstance=().unref(registers.context,removeContext);//no arg
					};
					let ans <-> registers.context+1;
					let ret <-> registers.context+2;
					pop r ans;
					sp+=1;//skip ret1
					pop r registers.context;//parent
					pop r ret;//ret
					sp+=1;//skip arg;
					[sp] = r ans;//replace context with ans
					//assert: stack = [ans]
				jump->r ret;
			};
			let makeContextInstance= ..prototype.makeContextInstance;
			def ..prototype.makeContextInstance;
		};
	};
};
def let program{
	let main=parse(lamda,lamda);
	sp = 0x800;//this is for the emulator
	call main;
	let counterLabel;
	def let end{
		let printStr(str,clearReg){
			let i=-1;
			repeat !!clearReg r0=0;
			repeat str..length:send r0 #(0xffff&str[i+=1]);
		};
		let printHexFromMemory(intAddress,clearReg)={//:#([int])=>{@}
			repeat !!clearReg r0=0;
			let int=1;//:%
			let char=2;//:% & [0x0-0xf]
			%int=[intAddress];
			let i=-1;
			repeat 4:{
				r char=#("0"&0xffff>>4);
				r int<<4;
				r char<<=4 +internal;
				send r0 r char;
			}
		};
		//printStr("\n\c0fmemory used:",{});
		//printHexFromMemory(counterLabel);
		hault;
		jump->0;
	};
	def main;
	#let counter;
	#heap[-1][-1]..splice(1,0,{#def counterLabel;0 #def counter;});
	#heap.malloc..splice(0,0,{[counter]+=1});
	#heap.free..splice(0,0,{[counter]-=1});
	def heap;
};